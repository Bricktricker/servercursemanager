plugins {
    id 'maven-publish'
    id 'java-library'
    id 'eclipse'
}

group 'bricktricker'
project.version = '1.1.0'
ext.forge_version = '37.0.104'
ext.mc_version = '1.19.1'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'forge'
        url = 'https://maven.minecraftforge.net'
    }
}

ext.sharedManifest = manifest {
    attributes(['Class-Path': 'forge.jar',
     'Main-Class': 'net.minecraftforge.server.ServerMain',
     'ServerLaunchArgs': "--gameDir . --launchTarget fmlserver --fml.forgeVersion ${forge_version} --fml.mcpVersion 20210706.113038 --fml.mcVersion ${mc_version} --fml.forgeGroup net.minecraftforge"
    ],
    )
    attributes(
            ['Specification-Title'     : 'servercursemanager',
             'Automatic-Module-Name' : 'serverpacklocator',
             'Specification-Vendor'    : 'bricktricker',
             'Specification-Version'   : '1',
             'Implementation-Title'    : project.name,
             'Implementation-Vendor'   : 'bricktricker',
             'Implementation-Version'  : "${version}",
             'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],
            'bricktricker/servercursemanager/')
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
    from 'COPYRIGHT'
    from 'LICENSE'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

dependencies {
    implementation('cpw.mods:modlauncher:10.0.8')
    implementation('net.minecraftforge:forgespi:6.0.2')
    implementation('com.electronwill.night-config:core:3.6.4')
    implementation('com.electronwill.night-config:toml:3.6.4')
    implementation('com.google.code.gson:gson:2.8.9')
    
    implementation('io.netty:netty-common:4.1.77.Final')
    implementation('io.netty:netty-buffer:4.1.77.Final')
    implementation('io.netty:netty-handler:4.1.77.Final')
    implementation('io.netty:netty-codec:4.1.77.Final')
    implementation('io.netty:netty-codec-http:4.1.77.Final') // Needs shadowing, rest is included in Minecraft
    
    implementation('org.apache.logging.log4j:log4j-api:2.17.0')
    implementation('org.apache.commons:commons-lang3:3.12.0')
}

task zip(type: Zip, dependsOn: jar) {
    archiveClassifier = 'bundle'
    from (jar.outputs) { into ("libraries/bricktricker/servercursemanager/${project.version}/") }
    from (file('servercursemanager.json')) {
        filter { line -> line
                .replace('@version@', "${project.version}")
                .replace('@forgeversion@', "${forge_version}")
                .replace('@mcversion@', "${mc_version}")
        }
        rename { String fileName ->
               "${mc_version}-" + fileName
        }
        into ("versions/${mc_version}-servercursemanager/")
    }
}

artifacts {
    archives zip
    archives jar
    archives sourcesJar
}

