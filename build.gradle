plugins {
    id 'maven-publish'
    id 'java-library'
    id 'eclipse'
}

group 'bricktricker'
project.version = '1.0.1'
ext.forge_version = '36.1.23'
ext.mc_version = '1.16.5'

targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'forge'
        url = 'https://files.minecraftforge.net/maven'
    }
}

ext.sharedManifest = manifest {
    attributes(['Class-Path': 'forge.jar',
     'Main-Class': 'net.minecraftforge.server.ServerMain',
     'ServerLaunchArgs': "--gameDir . --launchTarget fmlserver --fml.forgeVersion ${forge_version} --fml.mcpVersion 20190829.143755 --fml.mcVersion ${mc_version} --fml.forgeGroup net.minecraftforge"
    ],
    )
    attributes(
            ['Specification-Title'     : 'servercursemanager',
             'Specification-Vendor'    : 'bricktricker',
             'Specification-Version'   : '1',
             'Implementation-Title'    : project.name,
             'Implementation-Vendor'   : 'bricktricker',
             'Implementation-Version'  : "${version}",
             'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],
            'bricktricker/servercursemanager/')
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
    from 'COPYRIGHT'
    from 'LICENSE'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}


dependencies {
    implementation('cpw.mods:modlauncher:8.0.+')
    implementation('net.minecraftforge:forgespi:3.2.+')
    implementation('com.electronwill.night-config:core:3.6.3')
    implementation('com.electronwill.night-config:toml:3.6.3')
    implementation('com.google.code.gson:gson:2.8.0')
    implementation('io.netty:netty-all:4.1.25.Final')
    implementation('org.apache.logging.log4j:log4j-api:2.11.2')
    implementation('org.apache.commons:commons-lang3:3.8.1')
}

task zip(type: Zip, dependsOn: jar) {
    archiveClassifier = 'bundle'
    from (jar.outputs) { into ("libraries/bricktricker/servercursemanager/${project.version}/") }
    from (file('servercursemanager.json')) {
        filter { line -> line
                .replace('@version@', "${project.version}")
                .replace('@forgeversion@', "${forge_version}")
                .replace('@mcversion@', "${mc_version}")
        }
        rename { String fileName ->
               "${mc_version}-" + fileName
        }
        into ("versions/${mc_version}-servercursemanager/")
    }
}

artifacts {
    archives zip
    archives jar
    archives sourcesJar
}

compileJava {
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true
    options.forkOptions.executable = 'javac'
}
